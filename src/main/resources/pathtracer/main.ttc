include "../std/image.ttc"
include "../std/std.ttc"

include "color.ttc"
include "hittable.ttc"
include "hittable_list.ttc"
include "ray.ttc"
include "sphere.ttc"
include "vec3.ttc"

def infinity = 1 / 0
def pi = 3.1415926535897932385

def degrees_to_radians(degrees) = degrees * pi / 180

def ray_color(r, world) = (
    def rec = hit_record:new()
    if [hittable:hit(world, r, 0, infinity, rec)] (
        vec3:nmul(0.5, vec3:add(rec.normal, vec3:new(1, 1, 1)))
    )
    else (
        def unit_direction = vec3:unit_vector(r.direction)
        def a = 0.5 * (unit_direction[1] + 1)
        vec3:add(vec3:nmul(1 - a, vec3:new(1, 1, 1)), vec3:nmul(a, vec3:new(0.5, 0.7, 1.0)))
    )
)

def main(?) = (

    def aspect_ratio = 16 / 9
    def image_width = 400

    def image_height = floor(image_width / aspect_ratio)
    image_height = if [image_height < 1] 1 else image_height

    def image = imgCreate(image_width, image_height)

    def world = hittable_list:new()
    hittable_list:add(world, sphere:new(vec3:new(0, 0, -1), 0.5))
    hittable_list:add(world, sphere:new(vec3:new(0, -100.5, -1), 100))

    def focal_length = 1
    def viewport_height = 2
    def viewport_width = viewport_height * image_width / image_height
    def camera_center = vec3:new(0, 0, 0)

    def viewport_u = vec3:new(viewport_width, 0, 0)
    def viewport_v = vec3:new(0, -viewport_height, 0)

    def pixel_delta_u = vec3:div(viewport_u, image_width)
    def pixel_delta_v = vec3:div(viewport_v, image_height)

    def viewport_upper_left = vec3:sub(vec3:sub(vec3:sub(camera_center, vec3:new(0, 0, focal_length)), vec3:div(viewport_u, 2)), vec3:div(viewport_v, 2))
    def pixel00_loc = vec3:add(viewport_upper_left, vec3:nmul(0.5, vec3:add(pixel_delta_u, pixel_delta_v)))

    for [0, image_height] -> j (
        printf("\rScanlines remaining: %5.0f", image_height - j)
        for [0, image_width] -> i (
            def pixel_center = vec3:add(vec3:add(pixel00_loc, vec3:nmul(i, pixel_delta_u)), vec3:nmul(j, pixel_delta_v))
            def ray_direction = vec3:sub(pixel_center, camera_center)
            def r = ray:new(camera_center, ray_direction)

            def pixel_color = ray_color(r, world)
            imgSetPixel(image, i, j, color:int(pixel_color))
        )
    )

    printf("\rWriting image...          ")
    imgWrite("out.ppm", image)
    printf("\rDone.           %n")

    0
)
